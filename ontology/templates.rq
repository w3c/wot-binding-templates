prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix st: <http://ns.inria.fr/sparql-template/>
prefix schema: <http://schema.org/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix vann: <http://purl.org/vocab/vann/>
prefix dct: <http://purl.org/dc/terms/>
prefix : <http://w3c.github.io/wot-binding-templates/ontology#>

template :main(?ns) {
    format {
"""
<!DOCTYPE html>
<html>
    <head>
    <meta charset='utf-8'>
    <script src='https://www.w3.org/Tools/respec/respec-w3c-common' class='remove'></script>
    <script class='remove'>
        var respecConfig = {
            specStatus: "ED",
            editors: [%s],
            edDraftURI: "%s",
            shortName: "wot-binding-templates"
        };
    </script>
    <title>%s</title>
    </head>
    <body>
        <section id='abstract'>
            <p>%s</p>
        </section>
        <section id='sotd'>
            <p>
                <em>This document is a work in progress</em>
            </p>
        </section>
        <section>
            <h2>Introduction</h2>

            <p>
                To use in TD forms. 
            </p>
        </section>

        %s
    </body>
</html>
"""
        st:call-template(:editor, ?o)
        ?o
        ?title
        ?desc
        st:call-template(:axioms, ?o, ?prefix)
    }
} where {
    ?o a owl:Ontology ;
       rdfs:label ?title ;
       rdfs:comment ?desc ;
       vann:preferredNamespacePrefix ?prefix
}

template :editor(?o) {
    "{"
    "name:" ?name ","
    "company:" ?company ","
    "companyURL:" ?url
    "}" ; separator = ","
} where {
    { ?o dct:author ?ed } union { ?o dct:contributor ?ed }
    ?ed schema:name ?name ; schema:affiliation ?org .
    ?org schema:name ?company ; schema:url ?url .
}

template :axioms(?ns ?prefix) {
    "<section>"
        "<h2>Axiomatization</h2>"
        "<section>"
            "<h3>Classes</h3>"
            st:call-template(:classes, ?ns, ?prefix)
        "</section>"
        "<section>"
            "<h3>Object Properties</h3>"
            st:call-template(:properties, ?ns, ?prefix, owl:ObjectProperty)
        "</section>"
        "<section>"
            "<h3>Datatype Properties</h3>"
            st:call-template(:properties, ?ns, ?prefix, owl:DatatypeProperty)
        "</section>"
        st:call-template(:individuals-section, ?ns, ?prefix)
    "</section>"
} where {}

# FIXME see below. Why is the filter clause not proper?

template :individuals-section(?ns ?prefix) {
    "<section>"
        "<h3>Named Individuals</h3>"
        st:call-template(:individuals, ?ns, ?prefix)
    "</section>"
} where {
    filter exists {
        ?i a ?c .
        {
            # Use owl Enumeration classes
            ?c owl:equivalentClass ?enum .
            ?enum rdf:type owl:Class .
            ?emum owl:oneOf ?x .
        } union {
            # Use schema Enumeration classes
            ?c rdfs:subClassOf schema:Enumeration
        }
        #filter (strstarts(str(?i), str(?ns)))
    }
}

template :classes(?ns ?prefix) {
    "<section>"
        st:call-template(:term-header, ?c, ?prefix)
        "<table class=\"def\">"
            "<tbody>"
                st:call-template(:class-superclass, ?c, ?prefix)
                st:call-template(:class-subclasses, ?c, ?prefix)
                st:call-template(:class-domain-all, ?c, ?prefix)
                st:call-template(:class-range-all, ?c, ?prefix)
                st:call-template(:enumeration-members, ?c, ?prefix)
            "</tbody>"
        "</table>"
    "</section>"
} where {
    ?c a owl:Class ;
       rdfs:comment ?desc .
    filter (strstarts(str(?c), str(?ns)))
    bind (strafter(str(?c), "#") as ?label)
} order by ?c

template :class-superclass(?c ?prefix) {
    "<tr>"
        "<td>Sub-class of</td>"
        format {
            "<td>%s</td>"
            st:call-template(:term-link, ?sc, ?prefix)
        }
    "</tr>"
} where {
    ?c rdfs:subClassOf ?sc .
    ?sc a owl:Class .
}

template :class-subclasses(?c ?prefix) {
    "<tr>"
        "<td>Super-class of</td>"
        format {
            "<td>%s</td>"
            st:call-template(:class-subclass, ?c, ?prefix)
        }
    "</tr>"
} where {
    filter exists {
        ?sc rdfs:subClassOf ?c .
        ?sc a owl:Class .
    }
}

template :class-subclass(?c ?prefix) {
    st:call-template(:term-link, ?sc, ?prefix) ; separator = "<br>"
} where {
    ?sc rdfs:subClassOf ?c .
    ?sc a owl:Class .
} order by ?sc

template :class-domain-all(?c ?prefix) {
    "<tr>"
        "<td>In the domainÂ of</td>"
        "<td>" st:call-template(:class-domain, ?c, ?prefix) "</td>"
    "</tr>"
} where {
    filter exists {
        ?p schema:domainIncludes ?c .
    }
}

template :class-domain(?c ?prefix) {
    st:call-template(:term-link, ?p, ?prefix) ; separator = "<br>"
} where {
    ?p schema:domainIncludes ?c .
} order by ?p

template :class-range-all(?c ?prefix) {
    "<tr>"
        "<td>In the range of</td>"
        "<td>" st:call-template(:class-range, ?c, ?prefix) "</td>"
    "</tr>"
} where {
    filter exists {
        ?p schema:rangeIncludes ?c .
    }
}

template :class-range(?c ?prefix) {
    st:call-template(:term-link, ?p, ?prefix) ; separator = "<br>"
} where {
    ?p schema:rangeIncludes ?c .
} order by ?p

template :enumeration-members(?c ?prefix) {
    "<tr>"
        "<td>Enumeration members</td>"
        "<td>" st:call-template(:enumeration-member, ?c, ?prefix) "</td>"
    "</tr>"
} where {
    {
        # Use owl Enumeration classes
    ?c owl:equivalentClass ?enum .
        ?enum rdf:type owl:Class .
        ?emum owl:oneOf ?x .
    } union {
        # Use schema Enumeration classes
        ?c rdfs:subClassOf schema:Enumeration
    }
}

template :enumeration-member(?c ?prefix) {
    st:call-template(:term-link, ?member, ?prefix) ; separator = "<br>"
} where {
   {
        # Use owl Enumeration classes
    ?c owl:equivalentClass ?enum .
        ?enum rdf:type owl:Class .
        ?enum owl:oneOf ?member .
    } union {
        # Use schema Enumeration classes
        ?c rdfs:subClassOf schema:Enumeration .
    ?member a ?c .
    }
} order by ?member

template :properties(?ns ?prefix ?type) {
    "<section>"
        st:call-template(:term-header, ?p, ?prefix)
        "<table class=\"def\">"
            "<tbody>"
                st:call-template(:property-domain-all, ?p, ?prefix)
                st:call-template(:property-range-all, ?p, ?prefix)
                st:call-template(:property-domain-all-owl, ?p, ?prefix)
                st:call-template(:property-range-all-owl, ?p, ?prefix)
            "</tbody>"
        "</table>"
    "</section>"
} where {
    ?p a ?type ;
       rdfs:comment ?desc .
    filter (strstarts(str(?p), str(?ns)))
    bind (strafter(str(?p), "#") as ?label)
} order by ?p

template :property-domain-all(?p ?prefix) {
    "<tr>"
        "<td>Domain includes</td>"
        format {
            "<td>%s</td>"
            st:call-template(:property-domain, ?p, ?prefix)
        }
    "</tr>"
} where {
    filter exists {
        ?p schema:domainIncludes ?dom .
    }
}

template :property-domain-all-owl(?p ?prefix) {
    "<tr>"
        "<td>Domain</td>"
        format {
            "<td>%s</td>"
            st:call-template(:property-domain, ?p, ?prefix)
        }
    "</tr>"
} where {
    filter exists {
        ?p rdfs:domain ?dom .
    }
}

template :property-domain(?p ?prefix) {
    st:call-template(:term-link, ?dom, ?prefix) ; separator = "<br>"
} where {
    { ?p rdfs:domain ?dom . }
    union
    { ?p schema:domainIncludes ?dom . }
    filter (isIRI(?dom))
} order by ?dom

template :property-range-all(?p ?prefix) {
    "<tr>"
        "<td>Range includes</td>"
        format {
            "<td>%s</td>"
            st:call-template(:property-range, ?p, ?prefix)
        }
    "</tr>"
} where {
    filter exists {
        ?p schema:rangeIncludes ?range .
    }
}

template :property-range-all-owl(?p ?prefix) {
    "<tr>"
        "<td>Range</td>"
        format {
            "<td>%s</td>"
            st:call-template(:property-range, ?p, ?prefix)
        }
    "</tr>"
} where {
    filter exists {
        ?p rdfs:range ?range .
    }
}

template :property-range(?p ?prefix) {
    st:call-template(:term-link, ?range, ?prefix) ; separator = "<br>"
} where {
    { ?p rdfs:range ?range . }
    union
    { ?p schema:rangeIncludes ?range . }
    filter (isIRI(?range))
} order by ?range

template :individuals(?ns ?prefix) {
    "<section>"
        st:call-template(:term-header, ?i, ?prefix)
        "<table class=\"def\">"
            "<tbody>"
                "<tr>"
                    "<td>Instance of</td>"
                    "<td>" st:call-template(:term-link, ?c, ?prefix) "</td>"
                "</tr>"
            "</tbody>"
        "</table>"
    "</section>"
} where {
    ?i a ?c .
    {
    ?c owl:equivalentClass ?enum .
        ?enum rdf:type owl:Class .
        ?emum owl:oneOf ?x .
    } union {
        ?c rdfs:subClassOf schema:Enumeration
    }
    filter (strstarts(str(?i), str(?ns)))
}

template :term-header(?term ?prefix) {
    format { "<h4>%s</h4>" ?label }
    format { "<p>IRI: <code>%s</code></p>" ?term }
    format { "<span>%s</span>" ?desc }
} where {
    ?term rdfs:comment ?desc
    optional { ?term rdfs:label ?l }
    bind (if(bound(?l), ?l, strafter(str(?term), "#")) as ?label)
}

# FIXME see below

template :term-link(?term ?defaultPrefix) {
    format {
        "<code><a href=\"%s\">%s</a></code>"
        if(?prefix = ?defaultPrefix,
           concat("#", replace(lcase(?label), " ", "-", "g")),
           ?term)
        ?label
        # if(?curie,
        #     # FIXME schema.org label not correctly extracted if in WHERE clause?
        #     concat(?prefix, ":", if(strstarts(str(?term), str(schema:)), strafter(str(?term), str(schema:)), ?label)),
        #     ?label)
    }
} where {
    optional { ?term rdfs:label ?l }
    bind (if(bound(?l), ?l, strafter(str(?term), "#")) as ?label)
    # Note: if the prefix is not found the render throws the following error:
    # Error: Cannot resolve relative IRI  because no base IRI was set.render.js:72
    # To solve the error add an optional block as below
    optional {
        ?o a owl:Ontology ;
           vann:preferredNamespacePrefix ?prefix .
        filter (strstarts(str(?term), str(?o)))
    }
     optional {
         bind ("xsd" as ?prefix)
        filter (strstarts(str(?term), str(xsd:)))
    }
    # optional {
    #     bind ("schema" as ?prefix)
    #     filter (strstarts(str(?term), str(schema:)))
    # }
    bind (bound(?prefix) as ?curie)
}